states = {
  'launched': { state_class: 'fa-rocket', button_class: 'btn btn-primary spinner' },
  'ready': { state_class: 'fa-cubes', button_class: 'btn btn-success spinner' },
  'running': { state_class: 'fa-spin fa-cog', button_class: 'btn btn-warning spinner' },
  'stopped': { state_class: 'fa-moon-o', button_class: 'btn btn-danger spinner'}
}

app = angular.module('levels', [])

app.controller('MonitorController', [ '$http', ($http)->
  @levels = []
  @states = states
  @sending = false
  controller = this

  @loadLevelInfo = () ->
    $http.get('/admin/api/v1/levels.json').success (data) ->
      controller.levels = data

  @stateClass = (state) ->
    if state?
      @states[state].state_class
    else
      'fa-star'

  @addLevel = () ->
    @levels.unshift({})

  @loadLevelInfo()
])

app.controller('LevelPanelController', [ '$http', '$scope', ($http, $scope)->
  @config_files = []
  controller = this

  @loadConfigFiles = () ->
    $http.get('/admin/api/v1/config_files.json').success (data) ->
      controller.config_files = data

  # TODO move to model
  @hasState = (state) ->
    if typeof(state) == 'string'
      $scope.level.state is state
    else
      state.indexOf($scope.level.state) > -1

  @launchLevel = () ->
    @sending = true
    $http.post('/admin/api/v1/levels.json', {level: {name: $scope.level.name}})
      .success(@updateLevel)

  @buildLevel = () ->
    @sending = true
    $http.put('/admin/api/v1/levels/'+$scope.level.id+'/build.json', {level: {config: $scope.level.config_file}})
      .success(@updateLevel)

  @runLevel = () ->
    @sending = true
    $http.put('/admin/api/v1/levels/'+$scope.level.id+'/run.json')
      .success(@updateLevel)

  @stopLevel = () ->
    @sending = true
    $http.put('/admin/api/v1/levels/'+$scope.level.id+'/stop.json')
      .success(@updateLevel)

  @joinLevel = () ->
    @sending = true
    $http.put('/admin/api/v1/levels/'+$scope.level.id+'/join.json')
      .success (data) ->
        window.location.href = '/levels/'+$scope.level.id+'/map.html'

  @updateLevel = (data) =>
    $scope.level = data
    @sending = false

  @removeLevel = (level) ->
    @sending = true
    $http.delete('/admin/api/v1/levels/'+level.id)
      .success (data) ->
        index = $scope.monitorCtrl.levels.findIndex (item) ->
          item.name == level.name
        if index != -1
          $scope.monitorCtrl.levels.splice(index, 1)
        controller.sending = false

  @loadConfigFiles()

])

app.directive('createLevelButton', ->
  restrict: 'E',
  templateUrl: '<%= asset_path("create_level_button.html") %>'
)

app.directive('newLevelForm', ->
  restrict: 'E',
  templateUrl: '<%= asset_path("new_level_form.html") %>'
)

app.directive('levelActions', ->
  restrict: 'E',
  templateUrl: '<%= asset_path("level_actions.html") %>'
)
