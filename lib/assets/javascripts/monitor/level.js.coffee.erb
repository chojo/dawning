states = {
  'launched': { state_class: 'fa-rocket', button_class: 'btn btn-primary spinner' },
  'ready': { state_class: 'fa-cubes', button_class: 'btn btn-success spinner' },
  'running': { state_class: 'fa-spin fa-cog', button_class: 'btn btn-warning spinner' },
  'stopped': { state_class: 'fa-moon-o', button_class: 'btn btn-danger spinner'}
}

app = angular.module('levels', [])

app.controller('MonitorController', [ '$scope', '$http', ($scope, $http)->
  $scope.levels = []
  @sending = false

  @loadLevelInfo = () ->
    $http.get('/admin/api/v1/levels.json').success (data) ->
      $scope.levels = data

  @addLevel = () ->
    $scope.levels.unshift({})

  @removeLevel = (level) ->
    index = $scope.levels.findIndex (item) ->
      item.name == level.name
    if index != -1
      $scope.levels.splice(index, 1)

  @loadLevelInfo()
])

app.controller('LevelPanelController', [ '$scope', '$http', ($scope, $http)->
  @config_files = []
  @states = states
  controller = this

  @stateClass = (state) ->
    if state?
      @states[state].state_class
    else
      'fa-star'

  @loadConfigFiles = () ->
    $http.get('/admin/api/v1/config_files.json').success (data) ->
      controller.config_files = data

  # TODO move to model
  @hasState = (state) ->
    if typeof(state) == 'string'
      $scope.level.state is state
    else
      state.indexOf($scope.level.state) > -1

  @launchLevel = () ->
    @sending = 'launch'
    $http.post('/admin/api/v1/levels.json', {level: {name: $scope.new_level_name}})
      .success(@updateLevel)

  @buildLevel = () ->
    @sending = 'build'
    $http.put('/admin/api/v1/levels/'+$scope.level.id+'/build.json', {level: {config: $scope.level.config_file}})
      .success(@updateLevel)

  @runLevel = () ->
    @sending = 'run'
    $http.put('/admin/api/v1/levels/'+$scope.level.id+'/run.json')
      .success(@updateLevel)

  @stopLevel = () ->
    @sending = 'stop'
    $http.put('/admin/api/v1/levels/'+$scope.level.id+'/stop.json')
      .success(@updateLevel)

  @joinLevel = () ->
    @sending = 'join'
    $http.put('/admin/api/v1/levels/'+$scope.level.id+'/join.json')
      .success (data) ->
        window.location.href = '/levels/'+$scope.level.id+'/map.html'

  @updateLevel = (data) =>
    $scope.level = data
    @sending = false

  @removeLevel = (level) ->
    @sending = 'remove'
    $http.delete('/admin/api/v1/levels/'+level.id)
      .success (data) ->
        $scope.monitorCtrl.removeLevel(level)
        controller.sending = false

  @loadConfigFiles()

])

app.directive('createLevelButton', ->
  restrict: 'E',
  templateUrl: '<%= asset_path("create_level_button.html") %>'
)

app.directive('newLevelForm', ->
  restrict: 'E',
  templateUrl: '<%= asset_path("new_level_form.html") %>',
  link: () ->
    $('#level_name').focus()
)

app.directive('levelActions', ->
  restrict: 'E',
  templateUrl: '<%= asset_path("level_actions.html") %>'
)

app.directive('buildLevelForm', ->
  restrict: 'E',
  templateUrl: '<%= asset_path("build_level_form.html") %>'
)
